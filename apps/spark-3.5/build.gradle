plugins {
    // dependency in apps-spark-conventions
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'openhouse.apps-spark-conventions'
}

ext {
    log4jVersion = "2.20.0"

    sparkVersion = '3.5.2'
    icebergVersion = '1.5.2'
    sparkVersionSuffix = "-3.5"
    openhouseSparkRuntimeModule = ":integrations:spark${sparkVersionSuffix}:openhouse-spark${sparkVersionSuffix}-runtime_2.12"
    icebergSparkRuntimeModule = "org.apache.iceberg:iceberg-spark-runtime-3.5_2.12:${icebergVersion}"
    tablesTestFixturesModule = ":tables-test-fixtures-iceberg-1.5_2.12"
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == "org.apache.iceberg" &&
            details.requested.name != "iceberg-spark-runtime-3.1_2.12") {
            details.useVersion(icebergVersion)
        }
    }
}

dependencies {
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"

    implementation(project(':libs:datalayout')) {
        exclude group: 'org.apache.iceberg', module: 'iceberg-spark-runtime-3.1_2.12'
    }

    implementation("org.apache.iceberg:iceberg-bundled-guava:${icebergVersion}")
    implementation("org.apache.iceberg:iceberg-data:${icebergVersion}")
    implementation("org.apache.iceberg:iceberg-core:${icebergVersion}")
    implementation("org.apache.iceberg:iceberg-common:${icebergVersion}")



    compileOnly (project(path: openhouseSparkRuntimeModule)) {
        exclude group: 'io.netty'
        exclude group: 'org.apache.hadoop', module: 'hadoop-common'
        exclude group: 'org.apache.hadoop', module: 'hadoop-client'
    }

    implementation ('org.apache.spark:spark-core_2.12:' + sparkVersion) {
        exclude group: 'io.netty'
        exclude group: 'org.apache.hadoop', module: 'hadoop-common'
        exclude group: 'org.apache.hadoop', module: 'hadoop-client'
    }

    implementation ('org.apache.spark:spark-sql_2.12:' + sparkVersion) {
        exclude group: 'io.netty'
        exclude group: 'org.apache.hadoop', module: 'hadoop-common'
        exclude group: 'org.apache.hadoop', module: 'hadoop-client'
    }

    implementation (icebergSparkRuntimeModule) {
        exclude group: 'io.netty'
    }

    testImplementation (project(path: openhouseSparkRuntimeModule, configuration: 'shadow')) {
        exclude group: 'io.netty'
        exclude group: 'org.apache.hadoop', module: 'hadoop-common'
        exclude group: 'org.apache.hadoop', module: 'hadoop-client'
    }

    testImplementation(project(tablesTestFixturesModule)) {
        exclude group: "io.netty"
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', project(':apps:openhouse-spark-apps_2.12').sourceSets.main.java.srcDirs]
        }
    }
    test {
        java {
            srcDirs = ['src/test/java', project(':apps:openhouse-spark-apps_2.12').sourceSets.test.java.srcDirs]
        }
    }
}
